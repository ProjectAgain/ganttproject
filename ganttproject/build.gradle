buildscript {
    ext.kotlin_version = '1.4.+'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

ext {
    libDir = 'lib/core'
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/myJava'
}

dependencies {
    providedCompile project(path: ':biz.ganttproject.app.localization', configuration: 'exported')

    implementation files("lib/core/eclipsito.jar")
    implementation 'commons-codec:commons-codec:1.10'
    implementation 'org.apache.poi:poi:3.17'

    implementation 'ch.qos.logback:logback-classic:1.2.+'
    implementation 'commons-codec:commons-codec:1.+'
    implementation 'commons-io:commons-io:2.+'

    implementation 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'
    implementation 'com.evanlennick:retry4j:0.+'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.11.+'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.+'
    implementation 'com.jgoodies:jgoodies-common:1.8.+'
    implementation 'com.jgoodies:jgoodies-looks:2.7.+'
    implementation 'com.sandec:mdfx:0.+'
    implementation 'com.squareup.okhttp3:okhttp:3.+'

    implementation 'de.jensd:fontawesomefx-commons:11.+'
    implementation 'de.jensd:fontawesomefx-fontawesome:4.7.0-11'
    implementation 'de.jensd:fontawesomefx-materialicons:2.2.0-11'

    implementation 'net.java.balloontip:balloontip:1.2.4.1'
    implementation 'org.apache.commons:commons-lang3:3.+'

    implementation 'org.apache.httpcomponents:httpclient:4.5.+'
    implementation 'org.apache.httpcomponents:httpmime:4.5.+'
    implementation 'org.apache.poi:poi:4.+'
    implementation 'org.controlsfx:controlsfx:11.+'
    implementation 'org.nanohttpd:nanohttpd:2.3.+'
    implementation 'org.jdom:jdom:1.+'
//    implementation 'org.swinglabs:swingx:3.0.4'
//    implementation 'org.swinglabs.swingx:swingx-all:1.6.5-1'

    implementation fileTree(dir: 'lib/', include: ['*.jar'])

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.3.9'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.google.guava:guava:25.+"

    implementation group: 'com.itextpdf', name: 'itextpdf', version: '5.3.1'

    implementation 'org.mnode.ical4j:ical4j:1.+'

    implementation('net.sf.mpxj:mpxj:8.+') {
        exclude group: 'org.xerial', module: 'sqlite-jdbc'
        exclude group: 'com.jgoodies'
    }
    implementation 'javax.xml.bind:jaxb-api:2.3.+'
    implementation 'javax.xml:jaxb-impl:2.1'

    implementation fileTree(dir: project.ext.libDir, include: ['*.jar'])

    testImplementation group: 'org.easymock', name: 'easymock', version: '4.+'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.+'

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.+'

//    implementation "de.jensd:fontawesomefx-materialicons:2.2.+"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

task updateVersion {
    doFirst {
        def gpVersionFile = new File("${projectDir}/src/net/sourceforge/ganttproject/GPVersion.java")
        def gpversion = gpVersionFile.getText('UTF-8')
            .replaceAll('.*BUILD.NUMBER.*', "  public static String BUILD = \"${rootProject.buildNum}\"; // BUILD NUMBER")
        gpVersionFile.write(gpversion, 'UTF-8')
    }
}


task copyPluginFiles(dependsOn: jar) {
    doLast {
        println ">>> Installing $project.name"
        copy {
            into(new File(rootProject.pluginsDir, project.name))
            from(fileTree(".")) {
                include "plugin.xml"
            }
            from(fileTree("src/main/resources")) {
                include "resources/**.ttf"
                include "resources/**.properties"
                include "resources/calendar/**"
                include "resources/icons/**"
                include "resources/language/**"
                include "resources/xslfo/**"
                include "resources/xslt/**"
            }
            from(fileTree("src/main/resources/html-exporter")) {
                include "**"
                into "resources/"
            }
        }
        copy {
            into(new File(rootProject.pluginsDir, "${project.name}/lib/"))
            from(jar.outputs.getFiles().getFiles().flatten())
            from(configurations.compileClasspath.minus(configurations.providedCompile.resolve())) {
                include "*.jar"
            }
            rename { filename -> filename + ".lib" }
        }
        println "<<< $project.name"
    }
}

task copyEclipsito(type: Copy) {
    into(rootProject.distBinDir)
    from(fileTree("lib/core")) {
        include "eclipsito.jar"
    }
}

task copyPlugin(dependsOn: ['copyPluginFiles', 'copyEclipsito']) {
    doFirst {
        println "Copying $project.name to $rootProject.pluginsDir"
    }
}

mainClassName = "net.sourceforge.ganttproject.AppKt"

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

addPublishing(project)
publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'ganttproject'
            artifact jar
        }
    }
}
