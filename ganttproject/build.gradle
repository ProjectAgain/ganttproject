buildscript {
    ext.kotlin_version = '1.4.+'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'


ext {
    libDir = 'lib/core'
}

configurations {
    exportedLibs
    exported.extendsFrom archives, providedCompile, exportedLibs
    direct
    cloud {
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, JavaVersion.current().majorVersion.toInteger())
        }
    }
}

artifacts {
    cloud jar
}
dependencies {
    providedCompile project(path: ':biz.ganttproject.app.libs', configuration: 'exported')
    providedCompile project(path: ':biz.ganttproject.app.localization', configuration: 'exported')

    exportedLibs files("lib/core/eclipsito.jar")
    exportedLibs 'commons-codec:commons-codec:1.10'
    exportedLibs 'org.apache.poi:poi:3.17'

    direct 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    direct 'org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.3.9'
    direct "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    direct "com.google.guava:guava:25.+"

    cloud configurations.direct
    compile configurations.direct
    compile configurations.providedCompile

    compile group: 'com.itextpdf', name: 'itextpdf', version: '5.3.1'

    compile 'org.mnode.ical4j:ical4j:1.+'

    compile('net.sf.mpxj:mpxj:8.+') {
        exclude group: 'org.xerial', module: 'sqlite-jdbc'
        exclude group: 'com.jgoodies'
    }
    compile 'javax.xml.bind:jaxb-api:2.3.+'
    compile 'javax.xml:jaxb-impl:2.1'

    compile configurations.direct
    compile configurations.providedCompile
    compile configurations.exportedLibs
    compile fileTree(dir: project.ext.libDir, include: ['*.jar'])

    testImplementation group: 'org.easymock', name: 'easymock', version: '4.+'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.+'

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.+'

//    compile "de.jensd:fontawesomefx-materialicons:2.2.+"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

task updateVersion {
    doFirst {
        def gpVersionFile = new File("${projectDir}/src/net/sourceforge/ganttproject/GPVersion.java")
        def gpversion = gpVersionFile.getText('UTF-8')
            .replaceAll('.*BUILD.NUMBER.*', "  public static String BUILD = \"${rootProject.buildNum}\"; // BUILD NUMBER")
        gpVersionFile.write(gpversion, 'UTF-8')
    }
}


task copyPluginFiles(dependsOn: jar) {
    doLast {
        println ">>> Installing $project.name"
        copy {
            into(new File(rootProject.pluginsDir, project.name))
            from(fileTree(".")) {
                include "plugin.xml"
            }
            from(fileTree("src/main/resources")) {
                include "resources/**.ttf"
                include "resources/**.properties"
                include "resources/calendar/**"
                include "resources/icons/**"
                include "resources/language/**"
                include "resources/xslfo/**"
                include "resources/xslt/**"
            }
            from(fileTree("src/main/resources/html-exporter")) {
                include "**"
                into "resources/"
            }
        }
        copy {
            into(new File(rootProject.pluginsDir, "${project.name}/lib/"))
            from(jar.outputs.getFiles().getFiles().flatten())
            from(configurations.compileClasspath.minus(configurations.providedCompile.resolve())) {
                include "*.jar"
            }
            from(configurations.direct) {
                include "*.jar"
            }
            rename { filename -> filename + ".lib" }
        }
        println "<<< $project.name"
    }
}

task copyEclipsito(type: Copy) {
    into(rootProject.distBinDir)
    from(fileTree("lib/core")) {
        include "eclipsito.jar"
    }
}

task copyPlugin(dependsOn: ['copyPluginFiles', 'copyEclipsito']) {
    doFirst {
        println "Copying $project.name to $rootProject.pluginsDir"
    }
}

mainClassName = "net.sourceforge.ganttproject.AppKt"

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
}

addPublishing(project)
publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'ganttproject'
            artifact jar
        }
    }
}
